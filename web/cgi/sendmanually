#! /bin/csh

set ROOTDIR   = `pwd`
set ARCHDIR   = ./soplex
set SUBSTFILE = ./soplex.orig/soplex.cc
set SUBSTSTR  = "LICENSE_ID"
set USERS     = "./users"
set USERCOUNT = "./usercount"
set LOCK      = "${USERCOUNT}.LCK"

clear

echo "This is sendmanually V1.0."
echo "Starting manual transmission of SoPlex."
echo ""

if ( `pwd` != ${ROOTDIR} ) then
		echo "Sorry, not in distribution root directory."
		echo "cd to ${ROOTDIR} (terminating)."
		exit
endif
echo "Current location is SoPlex distribution root directory:"
pwd
echo ""

if ( ${#argv} == 0 ) then
		echo "Sorry, no recipient specified."
		echo "Usage is sendmanually <email>, try again (terminating)."
		echo ""
		exit
endif
set RECIPIENT = ${1}
echo "SoPlex will be mailed to ${RECIPIENT}."
echo ""

if ( !( -e ${USERS} ) ) then
		echo "Sorry, can't find user file ${USERS} (terminating)."
		exit
endif

if ( !( -e ${USERCOUNT} ) ) then
		echo "Sorry, can't find usercount file ${USERCOUNT} (terminating)."
		exit
endif
@ COUNT = `cat ${USERCOUNT}`
echo "Current number of SoPlex users is ${COUNT}."
@ COUNT = ${COUNT} + 1
echo "${RECIPIENT} is SoPlex user number ${COUNT}."
echo ""

if ( -e ${LOCK} ) then
		echo "Sorry, ${USERCOUNT} locked. Try again later (terminating)."
		exit
endif
touch ${LOCK}
echo "${USERCOUNT} locked:"
ls -lag ${USERCOUNT}*
echo ""

sed s/${SUBSTSTR}/${COUNT}/ ${SUBSTFILE} >! ${ARCHDIR}/soplex.cc
echo "License code installed:"
ls -lag ${ARCHDIR}/soplex.cc
diff ${SUBSTFILE} ${ARCHDIR}/soplex.cc
echo ""

echo "Creating archive soplex.uu . . ."
touch ${ARCHDIR}/.* ${ARCHDIR}/*
tar -cf ./soplex.tar ./soplex
gzip ./soplex.tar
uuencode ./soplex.tar.gz ./soplex.tar.gz >! soplex.uu
echo "Archive soplex.uu created:"
ls -lag ./soplex.[tu]*
echo ""

echo "Mailing archive soplex.uu . . ."
/usr/ucb/mail -s "SoPlex.uu" ${RECIPIENT} < ./soplex.uu
echo "Archive soplex.uu mailed to ${RECIPIENT}."
echo ""

rm -f ./soplex.uu
rm -f ./soplex.tar.gz
rm -f ${ARCHDIR}/soplex.cc
echo "Archive removed:"
ls -lag ./soplex.[tu]*
echo ""

echo ${COUNT} >! ${USERCOUNT}
echo "${USERCOUNT} updated:"
ls -lag ${USERCOUNT}
cat ${USERCOUNT}
echo ""

echo "MANUALLY_SENT:${RECIPIENT}:no_street:no_city:no_ZIP:no_country:on:on:${COUNT}" >> ${USERS}
echo "${USERS} updated:"
tail -1 ${USERS}
echo ""

rm -f ${LOCK}
echo "${USERCOUNT} unlocked:"
ls -lag ${USERCOUNT}*
echo ""

echo "Done."

